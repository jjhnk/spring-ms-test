plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.1'
	id 'io.spring.dependency-management' version '1.1.5'
}

group = 'hy.oltp.core'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

ext {
	mapstructVersion = '1.5.5.Final'
	springCloudVersion = '2023.0.2'
	querydslVersion = '5.0.0'
	annotationApiVersion = '3.0.0'
	jakartaVersion = '3.1.0'
	hibernateValidatorVersion = '8.0.0.Final'
}

repositories {
	mavenCentral()
	mavenLocal()
}

jar {
	enabled = false
}

dependencies {
	implementation 'hy.microservices.api:api:1.0.0'
	implementation 'hy.microservices.util:util:1.0.0'

	implementation 'io.micrometer:micrometer-tracing-bridge-brave'
	implementation 'io.zipkin.reporter2:zipkin-reporter-brave'
	implementation 'net.ttddyy.observation:datasource-micrometer-spring-boot:1.0.0'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.retry:spring-retry'
	implementation "com.querydsl:querydsl-apt:${querydslVersion}:jakarta"
	implementation "com.querydsl:querydsl-jpa:${querydslVersion}:jakarta"
	// implementation "jakarta.validation:jakarta.validation-api:${validationApiVersion}"
	implementation "org.mapstruct:mapstruct:${mapstructVersion}"
	implementation "org.hibernate.validator:hibernate-validator:${hibernateValidatorVersion}"

	compileOnly 'org.projectlombok:lombok'
	compileOnly "org.mapstruct:mapstruct-processor:${mapstructVersion}"
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}:jakarta"
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api:${jakartaVersion}"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
}

dependencyManagement {
	imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('generateQueryDSL', JavaCompile) {
	source = sourceSets.main.java
	classpath = sourceSets.main.compileClasspath
	options.annotationProcessorPath = configurations.annotationProcessor
	destinationDirectory.set(file("build/generated/source/apt/main"))
	options.compilerArgs = [
		"-proc:only",
		"-processor", "com.querydsl.apt.jpa.JPAAnnotationProcessor"
    ]
}